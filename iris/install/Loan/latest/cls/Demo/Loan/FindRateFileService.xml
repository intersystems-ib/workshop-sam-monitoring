<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Demo.Loan.FindRateFileService">
<ClassType/>
<ProcedureBlock>1</ProcedureBlock>
<Super>Ens.BusinessService</Super>
<TimeCreated>59278,43007.285</TimeCreated>

<Parameter name="ADAPTER">
<Description>
Name of the adapter class</Description>
<Default>EnsLib.File.InboundAdapter</Default>
</Parameter>

<Method name="OnProcessInput">
<FormalSpec>pFileStream:%FileCharacterStream,pOutput:%RegisteredObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set tInputFilename=$tr(pFileStream.Filename,"/","\")
	Set tInputFilename=$p(tInputFilename,"\",$l(tInputFilename,"\"))
	Set tRecordFilename=tInputFilename
	Set $p(tRecordFilename,".",$l(tRecordFilename,"."))="record"
	Set tApprovalFilename=tInputFilename
	Set $p(tApprovalFilename,".",$l(tApprovalFilename,"."))="approval"		
	#;
	$$$TRACE("found file "_tInputFilename)
	#;
	#; assuming the first line is 'amount:name:TaxID:nationality'
	Set tSubject=pFileStream.ReadLine()
	#;
	Set tApplication = ##class(Demo.Loan.Msg.Application).%New()
	Set tApplication.Amount=$p(tSubject,":",1)
	Set tApplication.Name=$p(tSubject,":",2)
	Set tApplication.TaxID=$p(tSubject,":",3)
	Set tApplication.Nationality=$p(tSubject,":",4)
	Set tApplication.BusinessOperationType="file"
	Set tApplication.Destination=tApprovalFilename
	Set tSC = ..SendRequestAsync("Demo.Loan.FindRateDecisionProcessCustom",tApplication)  Quit:$$$ISERR(tSC) tSC
	#;
	$$$TRACE("record number is "_tApplication.RecordNumber())
	#;
	Set tSendReply = ##class(Demo.Loan.Msg.SendReply).%New()
	Set tSendReply.Text=tApplication.GetRecordNumberText()
	Set tSendReply.Destination=tRecordFilename
	Set tSC=..SendRequestAsync("Demo.Loan.FindRateFileOperation",tSendReply)
	Quit tSC
]]></Implementation>
</Method>
</Class>
</Export>
