<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Demo.Loan.FindRateDecisionProcessCustom">
<ClassType>persistent</ClassType>
<IncludeCode>EnsUI</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>Ens.BusinessProcess</Super>
<TimeCreated>59277,34681.193</TimeCreated>

<Parameter name="INVOCATION">
<Description>
2 modes: Queue, InProc</Description>
<Default>Queue</Default>
</Parameter>

<Property name="BankName">
<Type>%String</Type>
</Property>

<Property name="IsApproved">
<Type>%Boolean</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Property name="InterestRate">
<Type>%Numeric</Type>
</Property>

<Method name="OnRequest">
<FormalSpec>request:Demo.Loan.Msg.Application,*response:Ens.Response</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	$$$TRACE("received application for "_request.Name)
	#;
	Set tSC = ..SendRequestAsync("Demo.Loan.BankUS",request)  Quit:$$$ISERR(tSC) tSC
	Set tSC = ..SendRequestAsync("Demo.Loan.BankSoprano",request)  Quit:$$$ISERR(tSC) tSC
	Set tSC = ..SendRequestAsync("Demo.Loan.BankManana",request)  Quit:$$$ISERR(tSC) tSC
	#;
	Quit ..SetTimer("PT5S")
]]></Implementation>
</Method>

<Method name="OnResponse">
<FormalSpec><![CDATA[request:Demo.Loan.Msg.Application,&response:Ens.Response,callrequest:Demo.Loan.Msg.Application,callresponse:Demo.Loan.Msg.Approval,pCompletionKey:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Quit:'callresponse.%Extends("Demo.Loan.Msg.Approval")||'callresponse.IsApproved $$$OK
	Quit:..IsApproved&&(..InterestRate<=callresponse.InterestRate) $$$OK
	Set ..BankName = callresponse.BankName
	Set ..IsApproved = callresponse.IsApproved
	Set ..InterestRate = callresponse.InterestRate
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="OnComplete">
<FormalSpec><![CDATA[request:Demo.Loan.Msg.Application,&response:Ens.Response]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	$$$TRACE("sync complete")
	#;
	Set tSendReply = ##class(Demo.Loan.Msg.SendReply).%New()
	Set tSendReply.Text = ..GetText()
	Set tSendReply.Destination = request.Destination
	If request.BusinessOperationType="mail" {
		Set tSC=..SendRequestAsync("Demo.Loan.FindRateEMailOperation",tSendReply,0)
	} Elseif request.BusinessOperationType="file" {
		Set tSC=..SendRequestAsync("Demo.Loan.FindRateFileOperation",tSendReply,0)
	} Elseif request.BusinessOperationType="term" {
		Set tSC=..SendRequestAsync("My Terminal Output",tSendReply,0)
	} Elseif request.BusinessOperationType="msmq" {
		Set tSC=..SendRequestAsync("Demo.Loan.FindRateMSMQOperation",tSendReply,0)
	} Elseif request.BusinessOperationType="mqseries" {
		Set tSC=..SendRequestAsync("Demo.Loan.FindRateMQSeriesOperation",tSendReply,0)
	}
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="OnError">
<FormalSpec><![CDATA[request:Ens.Request,&response:Ens.Response,callrequest:Ens.Request,pErrorStatus:%Status,pCompletionKey:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	$$$TRACE("received error "_$$$StatusDisplayString(pErrorStatus))
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="GetText">
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set tText=""
	Set tThank = $$FormatMessage^%occMessages(,"EnsDemo","FindRateThankyou","Thank you for applying with FindRate.")
	If ..IsApproved {
		Set tText=tText_$$FormatMessage^%occMessages(,"EnsDemo","FindRateIsApproved1","Your loan application was approved.")_$c(13,10)
		Set tText=tText_$$FormatMessage^%occMessages(,"EnsDemo","FindRateIsApproved2","The lowest interest rate available is at ")_..InterestRate_$$FormatMessage^%occMessages(,"EnsDemo","FindRateOfferedby","% offered by ")_..BankName_"."_$c(13,10)
		Set tText=tText_tThank_$c(13,10)
	} Else {
		Set tText=tText_$$FormatMessage^%occMessages(,"EnsDemo","FindRateIsDenied","Your loan application was denied.")_$c(13,10)
		Set tText=tText_tThank_$c(13,10)
	}
	Quit tText
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DefaultData>FindRateDecisionProcessDefaultData</DefaultData>
<Data name="FindRateDecisionProcessDefaultData">
<Subscript>"FindRateDecisionProcess"</Subscript>
<Value name="1">
<Value>BankName</Value>
</Value>
<Value name="2">
<Value>InterestRate</Value>
</Value>
<Value name="3">
<Value>IsApproved</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>
